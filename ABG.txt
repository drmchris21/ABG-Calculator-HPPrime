#pragma mode( separator(,;;) integer(h32) )

abg:={
"Compensated Metaboloc Acidosis";
"Uncompensated Metabolic Acidosis";
"Metabolic Acidosis with Respiratory Alcalosis";
"Acute Respiratory Acidosis over Chronic Respiratory Acidosis";
"Uncompensated Acute Respiratory Acidosis";
"Compensated Chronic Respiratory Acidosis";
"Metabolic Acidosis with Anion Gap";
"Metabolic Acidosis with Anion Gap and Acidosis without Anion Gap";
"Metabolic Acidosis with Anion Gap and Metabolic Acidosis";
"Compensated Metabolic Alcalosis";
"Metabolic Acidosis and Respiratory Acidosis";
"Metabolic Alcalosis and Respiratory Alcalosis";
"Partially Compensated Respiratory Alcalosis";
"Compensated Chronic Respiratory Alcalosis";
"Uncompensated Acute Respiratory Alcalosis";
"Partially Compensated Respiratory Acidosis";
"Partially Compensated Metabolic Acidosis";
"Partially Compensated Metabolic Alcalosis";
"Uncompensated Metabolic Alcalosis";
"Normal OR Compensated Respiratory Acidosis OR Compensated Metabolic Alcalosis";
"Normal OR Compensated Respiratory Alcalosis OR Compensated Metabolic Acidosis";
"Normal"};

Ph;pCO2;HCO3;
Na;Cl;AGAP;Alb;

acid1();
acid2();
acid3();
acid4();
acid5();
alc1();
alc2();
alc3();
alc4();
alc5();
Arac();
Amac();
agap();
Aralc();
Amalc();

EXPORT ABG(Ph_;pCO2_;HCO3_;Na_;Cl_;Alb_)
BEGIN
Ph:=Ph_; pCO2:=pCO2_;
HCO3:=HCO3_; Na:=Na_;
Cl:=Cl_; Alb:=Alb_;
PRINT();
PRINT("ABG Calculator results for...\nPh="+Ph+"\npCO2="+pCO2+"\nHCO3="+HCO3+"\nNa="+Na+"\nCl="+Cl+"\n");
PRINT("Albumin="+Alb+"\n");

IF Ph<7 OR Ph>7,7 THEN
PRINT("Is it alive?");
RETURN;
END;

AGAP:=Na-Cl-HCO3+2,3*(MAX(0;4,5-Alb));

(Ph<7,35 and HCO3>26)AND(acid1);
(Ph<7,35 and HCO3<=26 AND HCO3>=22)AND(acid2);
(Ph<7,35 and HCO3<22 AND pCO2>60)AND(acid3);
(Ph<7,35 and HCO3<22 AND pCO2<47)AND(acid4);
(Ph<7,35 and HCO3<22 AND pCO2<=60 AND pCO2>=47)AND(acid5);

IF Ph>=7,35 AND Ph<=7,45 AND HCO3>26 THEN PRINT(abg(20))
ELSE IF Ph>=7,35 AND Ph<=7,45 AND HCO3<=22 THEN PRINT(abg(21))
ELSE IF Ph>=7,35 AND Ph<=7,45 AND HCO3>22 AND HCO3<=26 THEN PRINT(abg(22))
END END END;

(Ph>=7,45 AND HCO3>26 AND pCO2>60)AND(alc3);
(Ph>=7,45 AND HCO3<22)AND(alc1);
(Ph>=7,45 AND HCO3<=26 AND HCO3>=22)AND(alc2);
(Ph>=7,45 AND HCO3>26 AND pCO2<47)AND(alc4);
(Ph>=7,45 AND HCO3>26 AND pCO2>=47)AND(alc5);
END;

acid1()
BEGIN
PRINT(abg(16));
Arac();
END;

acid2()
BEGIN
PRINT(abg(5));
Arac();
END;

acid3()
BEGIN
PRINT(abg(11));
Arac();
END;

acid4()
BEGIN
PRINT(abg(17));
Amac();
END;

acid5()
BEGIN
PRINT(abg(2));
Amac();
END;

Amac()
BEGIN
LOCAL x:=1,5*HCO3+8-pCO2;
IF x<=-0,5 THEN PRINT(abg(2))
ELSE IF x>=0,5 THEN PRINT(abg(12))
ELSE PRINT(abg(1))
END END;
IF AGAP<16 THEN RETURN; END;
LOCAL y:=IFTE(HCO3==24;24,01;HCO3);
x:=(AGAP-12)/(24-y);
PRINT("AG="+AGAP);
PRINT("x="+x);
IF x<1 THEN PRINT(abg(8))
ELSE IF x>2 THEN PRINT(abg(9))
ELSE PRINT(abg(7))
END END;
END;

Arac()
BEGIN
LOCAL y:=IFTE(pCO2==40;40,01;pCO2);
LOCAL x:=(10^(9-Ph)-10^1,6)/(y-40);
IF x>0,8 THEN PRINT(abg(5))
ELSE IF x<0,3 THEN PRINT(abg(6))
ELSE PRINT(abg(4))
END END;
END;

alc1()
BEGIN
PRINT(abg(13));
Aralc();
END;

alc2()
BEGIN
PRINT(abg(15));
Aralc();
END;

alc3()
BEGIN
PRINT(abg(18));
Amalc();
END;

alc4()
BEGIN
PRINT(abg(12));
Aralc();
END;

alc5()
BEGIN
PRINT(abg(19));
Amalc();
END;

Aralc()
BEGIN
LOCAL y:=IFTE(pCO2==40;40,01;pCO2);
LOCAL x:=(Ph-7,4)/(40-y);
IF x<0,0017 THEN PRINT(abg(14))
ELSE IF x>0,008 THEN PRINT(abg(15))
ELSE PRINT(abg(13))
END END;
END;

Amalc()
BEGIN
LOCAL x:=0,7*HCO3+21-pCO2;
IF x<-5 THEN PRINT(abg(3))
ELSE IF x>5 THEN PRINT(abg(12))
ELSE PRINT(abg(10))
END END;
END;
